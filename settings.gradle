// global build settings for the Minie project

rootProject.name = 'Minie'

dependencyResolutionManagement {
    repositories {
        //mavenLocal() // to find libraries installed locally
        mavenCentral() // to find libraries released to the Maven Central repository
    }
}

/*
 * Enumerate subdirectories in the project's root directory that contain a
 * "build.gradle" file. Any subdirectory that contains a "build.gradle" file is
 * automatically included as a subproject.
 */
def subDirs = rootDir.listFiles(
    new FileFilter() {
        boolean accept(File file) {
            return file.isDirectory() && new File(file, 'build.gradle').isFile()
        }
    }
)

subDirs.each { File sub ->
    include sub.name
}

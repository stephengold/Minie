/*
 * settings.gradle: global settings shared by all Minie subprojects
 */

dependencyResolutionManagement {
    repositories {
        //mavenLocal() // to find local SNAPSHOTs of libraries
        mavenCentral()
        //maven { url 'https://s01.oss.sonatype.org/content/groups/staging' } // to find libraries staged but not yet released
    }
}

rootProject.name = 'Minie'

/*
 * Enumerate subdirectories in the project's root directory that contain a
 * "build.gradle" file. Any subdirectory with a "build.gradle" file is
 * automatically a subproject of this project.
 */
def subDirs = rootDir.listFiles(
    new FileFilter() {
        boolean accept(File sub) {
            return sub.isDirectory() && new File(sub, 'build.gradle').isFile()
        }
    }
)

subDirs.each { File sub ->
    include sub.name
}

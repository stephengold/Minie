// Gradle settings and tasks common to all subprojects

apply plugin: 'java'

group = 'jme3utilities'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec) { // runtime options:
        classpath sourceSets.main.runtimeClasspath
        //doFirst { println commandLine }
        enableAssertions true
        //jvmArgs '-verbose:gc'
        //jvmArgs '-Xcheck:jni'
        jvmArgs '-Xms1024m', '-Xmx1024m'
        jvmArgs '-XX:+UseConcMarkSweepGC'
        //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
    }
}

ext { // current versions of the libraries
    heartVersion = '2.30.0for32'
    jme3utilitiesuiVersion = '0.7.5for32'
    jmonkeyengineVersion = '3.2.4-stable'
    minieVersion = '0.9.11for32'
}

repositories {
    //mavenLocal()
    maven { url 'https://dl.bintray.com/stephengold/jme3utilities' }
    maven { url 'https://dl.bintray.com/stephengold/org.jmonkeyengine' } // for testdata
    maven { url 'https://dl.bintray.com/riccardo/v-hacd' } // for v-hacd-java-bindings
    jcenter()
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes, description: 'Create a jar of source files.') {
    classifier = 'sources'
    from sourceSets.main.allJava
}
task javadocJar(type: Jar, dependsOn: javadoc, description: 'Create a jar of javadoc.') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives jar, sourcesJar, javadocJar
}
